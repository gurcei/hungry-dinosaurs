#output "hungrydinosaurs"
'------------------------

.declares
'--------
#declare k, z, north%, south%, east%, west%, allow%, res%
#declare a$,b$,c$,tok$
#declare current_room%, previous_room%
#declare inventory$(10),inv_count%=0,inv_idx%=0
#declare fed_dino_count% = 0

#declare present$(4), missing% = 0, game_over% = 0
present$(0) = "whistle"
present$(1) = "floppy-disk"
present$(2) = "mega65"
present$(3) = "program-listing"

#declare map_loc_to_objs$(4,2)
#declare map_idx%

#define GERMANY=0
#define TURKEY=1
#define THAILAND=2
#define VIETNAM=3
#define AUSTRALIA=4
#define MAX_ROOMS=5

#struct ROOM desc$, exits$, locs$, vart$

ROOM rooms(MAX_ROOMS) = [ {x5F}
 [ "You are trapped in Onion Cake's dirty smelly prison!", {x5F}
     "", "", "german.v", ], {x5F}
 [ "A Turkish dinosaur is bathing in the thermal waters.", {x5F}
     "S", "2", "turk.v", ], {x5F}
 [ "A Thai dinosuar is performing a traditional dance.", {x5F}
     "NE", "13", "thai.v", ], {x5F} 
 [ "A Vietnamese dinosaur is tending to the rice terraces.", {x5F}
     "WS", "24", "viet.v", ], {x5F}
 [ "An inebriated Australian dinosaur rests peacefully.", {x5F}
     "N", "3", "aus.v", ] {x5F}
]

#declare prog$(7), finished_lines%=0
prog$(0)="10 print "+chr$(34)+"i promise to onion cake that i will"+chr$(34)
prog$(1)="20 print "+chr$(34)+"- always write beautiful mega65 programs"+chr$(34)
prog$(2)="30 print "+chr$(34)+"- make use of delicious petscii characters"+chr$(34)
prog$(3)="40 print "+chr$(34)+"- make beautiful sid music with the 'play' command"+chr$(34)

goto main

.remove_from_loc
'---------------
  if map_idx%=1 then begin
    map_loc_to_objs$(current_room%,1) = ""
  bend:else if map_idx%=0 then begin
    map_loc_to_objs$(current_room%,0) = ""
    if map_loc_to_objs$(current_room%,1) <> "" then begin
      map_loc_to_objs$(current_room%,0) = map_loc_to_objs$(current_room%,1)
      map_loc_to_objs$(current_room%,1) = ""
    bend
  bend
  return

.take
'----
  gosub extract_token
  map_idx% = -1
  for k = 0 to 1
    c$ = map_loc_to_objs$(current_room%,k)
    if tok$ = left$(c$,3) then b$ = c$:map_idx% = k
  next k
  if left$(b$,3) = tok$ then begin
    allow% = 1
    if tok$="egg" then print "The eggplant stubbornly resists your attempt to take it!":allow%=0
    if b$="rice-plant" then print "The rice-plant refuses to relent to your will!":allow%=0
    if tok$="car" then print "The carnivorous-mite gnashes its teeth at you, forcing you to back down!":allow%=0
    if tok$="tig" then print "The tiger unleashes a ferocious growl, making you shiver in your boots!":allow%=0

    if allow%=1 then begin
      inventory$(inv_count%) = b$
      inv_count% = inv_count% + 1
      gosub remove_from_loc
      print "You take the ";b$;"..."
    bend
  bend:else begin
    print "You can't see that here!"
  bend
  return

.hit
'---
  gosub extract_token
  map_idx% = -1
  for k = 0 to 1
    c$ = map_loc_to_objs$(current_room%,k)
    if tok$ = left$(c$,3) then b$ = c$:map_idx% = k
  next k
  if left$(b$,3) = tok$ then begin
    if tok$="egg" then begin
      print "The impact of your fist causes it to become a fainted-eggplant."
      map_loc_to_objs$(current_room%,map_idx%)="fainted-eggplant"
    bend
    if tok$="ric" then begin
      print "You flatten the rice with your fist, forming a tasty rice-paper-roll."
      map_loc_to_objs$(current_room%,map_idx%)="rice-paper-roll"
    bend
    if tok$="car" then begin
      print "Your violent actions have made the mite rethink life, it is now a vegan-mite."
      map_loc_to_objs$(current_room%,map_idx%)="vegan-mite"
    bend
    if tok$="tig" then begin
      print "I think you hurt its feelings, it is now a crying-tiger..."
      map_loc_to_objs$(current_room%,map_idx%)="crying-tiger"
    bend
  bend:else begin
    print "You can't see that here!"
  bend
  return

.find_in_inventory
'-----------------
  res% = 0 : inv_idx% = -1
  if inv_count%=0 then return
  for k=0 to inv_count%-1
    if left$(inventory$(k),3) = tok$ then res% = 1:inv_idx% = k:b$=inventory$(k)
  next k
  return

.remove_from_inventory
'---------------------
  if inv_idx%=inv_count%-1 then inv_count%=inv_count%-1:return
  for k=inv_idx%+1 to inv_count%-1
    inventory$(k-1) = inventory$(k)
  next k
  inv_count% = inv_count% - 1
  return

.drop_present
'-----------
  if map_loc_to_objs$(current_room%,0) = "" then begin
    map_loc_to_objs$(current_room%,0) = present$(fed_dino_count%)
  bend:else begin
    map_loc_to_objs$(current_room%,1) = present$(fed_dino_count%)
  bend
  return

.give
'----
  gosub extract_token
  gosub find_in_inventory
  if res%=0 then print "You are not carrying that!":return
  if left$(b$,3) = tok$ then begin
    allow%=0
    if tok$="ric" and current_room%=VIETNAM then allow%=1
    if tok$="veg" and current_room%=AUSTRALIA then allow%=1
    if tok$="cry" and current_room%=THAILAND then allow%=1
    if tok$="fai" and current_room%=TURKEY then allow%=1
    if current_room%=GERMANY then begin
      if tok$="meg" then begin
        print "'Not yet, show me that you know how to use this beautiful machine, or else...'"
      bend:else begin
        print "Onion Cake glares at you, 'Stop wasting time and prove your skills to me!'"
      bend
      return
    bend
    if allow% = 0 then print "The dinosaur appreciates your kind gesture, but that isn't the food it likes"
    if allow% = 1 then begin
      gosub remove_from_inventory
      if current_room% = VIETNAM then begin
        print "The Vietnamese dinosaur devours the tasty rice-paper roll..."
      bend
      if current_room% = AUSTRALIA then begin
        print "The Australian dinosaur chomps down the scrumptious mite and then"
        print "falls back asleep..."
      bend
      if current_room% = THAILAND then begin
        print "The Thai dinosaur gracefully dines on the crying-tiger, and "
        print "then raises its clasped paws to its forehead to wai in thanks."
      bend
      if current_room% = TURKEY then begin
        print "The Turkish dinosaur enthusiastically rips into the tasty fainted-eggplant!"
      bend
      print "It drops a present for you to show its gratitude..."
      
      gosub drop_present
      fed_dino_count% = fed_dino_count% + 1
    bend
  bend

  return

.check_we_have_all_computer_items
'--------------------------------
  print "Onion cake looks over your inventory..."
  sleep 2
  missing%=0
  for z = 0 to 3
    tok$ = left$(present$(z), 3)
    gosub find_in_inventory
    if res%=0 then begin
      print "'Hmm... You seem to be missing a ";present$(z);"...'"
      missing%=1
    bend
  next z

  if missing%=1 then begin
    res%=0
    print
    print "I'm afraid this means that I need to shoot you now!"
    print
    print "BANG!"
    return
  bend

  print "'Looks like you've got it all...'"
  print "'Now, demonstrate your MEGA65 coding skills to me, otherwise...'"
  gosub press_a_key
  res% = 1
  return

.captured_by_onion_cake
'----------------------
  print "Upon hearing your melodious whistling, Onion Cake arrives, holding you"
  print "at gunpoint, and forcefully escorting you to his dungeon in Germany..."
  current_room% = GERMANY
  gosub press_a_key

  print "'I finally caught the thief that stole my MEGA65!', Onion Cake exclaims!"
  print
  print "'If you show me your programming skills I will release you!' he offers."
  print
  print "'If not, I will shoot you!'"
  gosub press_a_key

  gosub check_we_have_all_computer_items
  if res%=0 then game_over%=1:gosub press_a_key:return
  return

.type_line
'---------
  input a$
  return

.assess_line
'-----------
  if a$=prog$(finished_lines%) then begin
    finished_lines% = finished_lines% + 1
    if finished_lines%=4 then begin
      print "'Well done, you completed the program!'"
    bend:else begin
      print "'Good, now for the next line!'"
    bend
  bend:else begin
    print "'No no! You made mistakes on this line!', Onion Cake roars"
    print "'Type it again properly, otherwise I will shoot you!'"
    print prog$(finished_lines%)
  bend
  return

.start_mega65
'------------
' draw mega65+listing
  do while finished_lines% < 4
    gosub type_line
    gosub assess_line
  loop

  print "'Impressive!', Onion Cake says with admiration!"
  print "'You have proven yourself worthy to keep alive! I hereby release you!"
  print "Remember your promise, otherwise I will find you and shoot you!'"
  gosub press_a_key

  print "You are free again! Congratulations, you won the game!"
  game_over%=1
  return

.use
'---
  gosub extract_token
  gosub find_in_inventory
  if res%=0 then print "You are not carrying that!":return
  if left$(b$,3) = tok$ then begin
    if tok$="whi" then gosub captured_by_onion_cake:return
    if tok$="meg" then begin
      if current_room%=GERMANY then begin
        gosub start_mega65
        game_over%=1
        return
      bend:else begin
        print "How could you think of playing with your MEGA65 when there are hungry"
        print "dinosaurs to feed?!"
        return
      bend
    bend
    print "You cannot use that!"
  bend
  return

.inventory
'---------
  print "You are carrying:"
  if inv_count% = 0 then print "  - nothing...":else begin
    for k=0 to inv_count%-1
      print "  - ";inventory$(k)
    next k
  bend

  return

.extract_token
'-------------
  k = instr(a$," ")
  tok$=left$(a$,3)
  if k>0 then begin
    a$=mid$(a$,k+1)
  bend:else begin
    a$=""
  bend
  return

.user_input
'----------
  input a$

  if a$="n" and north%<>-1 then current_room%=north%
  if a$="s" and south%<>-1 then current_room%=south%
  if a$="e" and east%<>-1 then current_room%=east%
  if a$="w" and west%<>-1 then current_room%=west%
  if a$="i" then gosub inventory
  if a$="x" then stop

  gosub extract_token
  if tok$="loo" then gosub show_room_text
  if tok$="tak" or tok$="get" then gosub take
  if tok$="inv" then gosub inventory
  if tok$="hit" then gosub hit
  if tok$="giv" then gosub give
  if tok$="use" then gosub use

  return

.show_objects
'------------
  res%=0
  for k=0 to 1
    a$ = map_loc_to_objs$(current_room%, k)
    b$ = left$(a$,1)
    c$ = "aeiou"
    if a$<>"" then begin
      if res%=0 then print "You see a";:res%=1:else print " and a";
      if instr(c$,b$)<>0 then print "n";
      print " ";a$;
    bend
  next k
  
  if res%<>0 then print " here."

  return

.show_room_text
'--------------
  print rooms_desc$(current_room%)

  gosub show_objects

  north%=-1:south%=-1:east%=-1:west%=-1
  print "Exits: ";
  a$ = rooms_exits$(current_room%)
  if len(a$) > 0 then begin
    for k=1 to len(a$)
      b$=mid$(a$,k,1)
      c$=mid$(rooms_locs$(current_room%),k,1)
      print b$;
      if k<>len(a$) then print ", ";
      if b$="N" then north%=val(c$)
      if b$="S" then south%=val(c$)
      if b$="E" then east%=val(c$)
      if b$="W" then west%=val(c$)
    next k
  bend
  if len(a$)=0 then print "none..."                         
  print
  return

.show_room_graphic
'-----------------
  return

.main_game
'---------
  gosub show_room_graphic
  gosub show_room_text
.next_input
  gosub user_input

  if game_over%=1 then print:print "GAME OVER":sleep 2:return
  if current_room%<>previous_room% then previous_room%=current_room%:goto main_game
  goto next_input
  return

.init_game
'---------
  print chr$(147);
  current_room% = VIETNAM
  previous_room% = current_room%
  inv_count% = 0
  fed_dino_count% = 0
  missing% = 0
  game_over% = 0
  finished_lines% = 0

  map_loc_to_objs$(TURKEY,0) = "rice-plant"
  map_loc_to_objs$(THAILAND,0) = "carnivorous-mite"
  map_loc_to_objs$(VIETNAM,0) = "eggplant"
  map_loc_to_objs$(AUSTRALIA,0) = "tiger"

  map_loc_to_objs$(TURKEY,1) = ""
  map_loc_to_objs$(THAILAND,1) = ""
  map_loc_to_objs$(VIETNAM,1) = ""
  map_loc_to_objs$(AUSTRALIA,1) = ""

  ' screen 0, 640, 400, 4
  return

.main
'----
  gosub show_title_page
  gosub init_game
  gosub main_game
  goto main
  end

.press_a_key
'-----------
  print
  print "[Press any key to continue]"
  get key a$
  return

.show_title_page
'---------------
  print chr$(147);
  print "Onion Cake and the Hungry Dinosaurs"
  print "==================================="
  print "  Coding & Music sequencing by Gurce Isikyildiz"
  print "  Dino Artistry by Ayca Isikyildiz"
  print
  print " I)nstructions"
  print " C)redits"
  print
  print " Press SPACE BAR to begin"
  get a$
  get key a$
  if a$="i" then gosub instructions:goto show_title_page
  if a$="c" then gosub credits:goto show_title_page
  if a$<>" " then goto show_title_page
  return

.instructions
'------------
  print chr$(147);
  print "Instructions (page 1 of 2)"
  print "============"
  print "The dinosaurs are hungry..."
  print
  print "...go and find them the food that they like!"
  print
  print "When you give them all the right food, they will reward you with a "
  print "new retro computer!"
  print
  print "...but beware of dirty old Onion Cake..."
  print
  print "...he is angry due to somebody stealing his favourite retro computer..."
  print
  print "Maybe you can appease his anger by returning it to him..."
  gosub press_a_key
  
  print chr$(147);
  print "Instructions (page 2 of 2)"
  print "============"
  print "This is a simple text adventure game accompanied by vector art drawn "
  print "(slowly) via BASIC 65 :) Artwork will be cached after 1st draw attempt completes."
  print
  print "Type simple VERB NOUN strings to interact with the game."
  print "  - E.g. TAKE MEGA65"
  print
  print "You can abbreviate any word to the first 3 letters."
  print "  - E.g. TAK MEG"
  print
  print "Game Vocabulary"
  print "---------------"
  print "- N, S, E, W   (to go North, South, East, West)"
  print "- I/INVENTORY"
  print "- GO <place>"
  print "- TAKE <object>"
  print "- USE <object>"
  print "- GIVE <object>"
  print "- HIT <object>"
  print "- JOIN <object>"
  print "- QUIT/EXIT"
  gosub press_a_key

  return

.credits
'-------
  print chr$(147);
  print "Credits"
  print "======="
  print "Coding and Music sequencing by Gurce Isikyildiz"
  print "Dino Artistry by Ayca Isikyildiz"
  print
  print "Song cover credits"
  print "------------------"
  print "Australia - ";chr$(34);"Skippy the Bush Kangaroo";chr$(34);
    print " - Eric Jupp"
  print "Germany - ";chr$(34);"99 Luftballons";chr$(34);" - NENA"
  print "Thailand - ";chr$(34);"Khaek Boratet Sawng Chan";chr$(34);
    print " - Unknown"
  ' youtu.be/HLqlZ2jGbDg
  print "Turkey - ";chr$(34);"Hatcam Cikmis Gul Dalina";chr$(34);
    print " - Unknown"
  print "Vietnam - ";chr$(34);"Tram to pick...";chr$(34);
    print " - Unknown"
    gosub press_a_key

  return
ÿ